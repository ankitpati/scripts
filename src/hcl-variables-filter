#!/usr/bin/env perl
# Sort HashiCorp Configuration Language (HCL) `variable` blocks from STDIN
#
# * STDIN must be `(terraform|packer) fmt`'d
# * Comments, if any, must be inside the braces
#
# Must look like this, or it will fail:
#
# variable "example" {
#   default = "example.com"
# }
#
# variable "example1" {
#   # comment
#   default = "1.example.com"
# }

use strict;
use warnings;
use autodie;

our $VERSION = 1.00;

# UTF8 for STDIN & STDOUT
use open qw(:std :utf8);

my $me = (split m{/}, $0)[-1];

die "Usage:\n\t$me < <variables.tf|variables.hcl>\n"
    if @ARGV;

my %variables;
my $current_variable;
foreach my $line (<STDIN>) {
    chomp $line;

    if ($line =~ /^\s*variable\s+"([^"]+)"\s*\{\s*$/) {
        $current_variable = $1;

        # necessary to store the `variable` line even if it has no body
        $variables{$current_variable} //= [];
    } else {
        push @{ $variables{$current_variable} }, $line;
    }
}

foreach my $variable (sort keys %variables) {
    my $body = join "\n", @{ $variables{$variable} };

    print <<"EOH";
variable "$variable" {
$body
EOH
}

__END__
