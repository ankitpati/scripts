#!/usr/bin/env perl

# Perl New Test

use strict;
use warnings;

use Cwd;
use File::Basename;
use File::Path;
use File::Spec::Functions 'abs2rel';

my $me = ${\(split m{/}, $0)[-1]};

die "Usage:\n\t$me <filename>... [--force]\n"
    if not @ARGV or (grep (/^--force$/, @ARGV) and @ARGV < 2);

my $force = grep /^--force$/, @ARGV;
@ARGV = grep !/^--force$/, @ARGV;

my $cwd = cwd;

my $gitroot;
$gitroot = eval { `git rev-parse --show-toplevel` };
chomp $gitroot;
die "$me: Unsupported working directory or `git` not on \$PATH!\n"
    if $@ or not $gitroot;

foreach (@ARGV) {
    s|^(?:.*/Acme/)?(.*)\.pm$|t/lib/TestsFor/Acme/$1/legacy.pm|;
    print("$_ exists. Use --force to overwrite.\n"), next
        if -e $_ and not $force;

    my $old_test = /\.t$/;
    chdir ($old_test ? $cwd : $gitroot);

    eval { mkpath dirname $_ };
    warn("Could not create required directories\n"), next if $@;
    open my $test_file, '>', $_ or warn("Could not create $_\n"), next;

    my $inc_path = abs2rel "$gitroot/acme/conf/", dirname $_;
    s@^.*?/Acme/|^t/|\.t$|(?:/legacy)?\.pm$@@g, s@/t/|/@::@g;

    if ($old_test) {
        print $test_file <<"EOF";
# Tests for Acme::$_.
# ----------------${\('-' x length)}-

use strict;
use warnings;

use File::Basename qw(dirname);
BEGIN { require( dirname(\$0) . '/$inc_path/include.pm' ); }

use Test::More tests => 1;
use Test::Exception;
use Test::MockModule;
use Test::MockObject;

my \$pkg;

BEGIN {
    \$pkg = 'Acme::$_';
    use_ok(\$pkg);
}
EOF
    }
    else {
        print $test_file <<"EOF";
package TestsFor::Acme::${_}::legacy;

use strict;
use warnings;

use File::Basename qw(dirname);
BEGIN { require( dirname(__FILE__) .
        '/$inc_path/include.pm' ); }

use Test::Class::Moose;
use Test::MockModule;
use Test::MockObject;

use Acme::$_;

my \$pkg;
sub test_startup {
    \$pkg = 'Acme::$_';
}

1;
EOF
    }

    close $test_file;
}
