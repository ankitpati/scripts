#!/usr/bin/env perl

use strict;
use warnings;
use autodie;

use List::AllUtils qw(uniq);
use IPC::Run qw(run);

my $me = (split m{/}, $0)[-1];
my $usage = <<"EOF";
Usage:
    $me [<--since|-s> <yyyy-mm-dd>] [<--perl-regexp|-p>] [<--committer|-c>] [<username>...]

Examples:
    $me ankit                      # contributions by ankit
    $me ''                         # contributions by All Users
    $me tiashaa --since 2020-05-01 # by tiashaa, since 01 May 2020
    $me tiashaa -s 2020-05-01      # `-s`, shorthand for `--since`
    $me ankit tiashaa              # contributions by multiple users
    $me '^ankit.*' -p              # use PCRE regex for matches
    $me tiashaa -c                 # match against committers, not authors
    $me ankit -c -p -s 2020-05-01  # specify multiple options
EOF

@ARGV or die $usage;

my $usertype = 'author';
my @regextype = qw(--fixed-strings);
my (@users, $since, @since);
for (my ($i, $argc) = (0, scalar @ARGV); $i < $argc; ++$i) {
    my $arg = $ARGV[$i];

    if ($arg =~ /^(?:--committer|-c)$/) {
        $usertype = 'committer';
    }
    elsif ($arg =~ /^(?:--perl-regexp|-p)$/) {
        @regextype = qw(--perl-regexp);
    }
    elsif ($arg =~ /^(?:--since|-s)$/) {
        $since = $ARGV[++$i] || ''; # `--since` is last argument!
        $since =~ /^\d{4}-\d{2}-\d{2}$/ or die $usage;
    }
    else {
        push @users, $arg;
    }
}

if ($since) {
    @since = "--since=$since";
    print "Since $since\n\n";
}

push @users, '' unless @users; # All Users, according to `git-log`
foreach my $user (@users) {
    my $log;

    run [qw(git log --no-merges --oneline --shortstat),
         "--$usertype=$user", @since, @regextype], '>', \$log;

    open my $log_fd, '<', \$log;

    my ($files, $commits, $insertions, $deletions);

    while (<$log_fd>) {
        if (/^[a-z0-9]+ /i) { ++$commits; next; }

        my ($ins) = /(\d+) insertion/;
        my ($del) = /(\d+) deletion/;

        $insertions += $ins || 0;
        $deletions  += $del || 0;
    }

    close $log_fd;

    $commits    ||= 0;
    $insertions ||= 0;
    $deletions  ||= 0;

    run [qw(git -c log.showSignature=false log --no-merges --name-only
            --pretty=), "--$usertype=$user", @since, @regextype], '>', \$log;

    $files = grep !/^$/, uniq split /\n/, $log;

    $user = 'All Users' unless $user;
    print <<"EOT";
$user
    Files      : $files
    Commits    : $commits
    Insertions : $insertions
    Deletions  : $deletions

EOT
}
