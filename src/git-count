#!/usr/bin/env perl

use strict;
use warnings;

use List::AllUtils qw(uniq);
use IPC::Run qw(run);

my $me = (split m{/}, $0)[-1];
my $usage = "Usage:\n\t$me [[--since|-s] <yyyy-mm-dd>] [<username>...]\n";

@ARGV or die $usage;

my (@users, $since);
for (my ($i, $argc) = (0, scalar @ARGV); $i < $argc; ++$i) {
    my $arg = $ARGV[$i];

    push (@users, $arg), next if $arg !~ /^(?:--since|-s)$/;

    ++$i;
    die $usage if $i == $argc; # `--since` is last argument!

    $since = $ARGV[$i];
}

if ($since) {
    $since =~ /^\d{4}-\d{2}-\d{2}$/ or die $usage;
    print "Since $since\n\n";
}
my @since = $since ? "--since=$since" : ();

push @users, '' unless @users; # All Users, according to `git-log`
foreach my $user (@users) {
    my $log;

    run [qw(git log --no-merges --oneline --shortstat),
         "--author=$user", @since], '>', \$log;

    open my $log_fd, '<', \$log;

    my ($files, $commits, $insertions, $deletions);

    while (<$log_fd>) {
        ++$commits, next if /^[a-z0-9]+ /i;

        my ($ins) = /(\d+) insertion/;
        my ($del) = /(\d+) deletion/;

        $insertions += $ins || 0, $deletions += $del || 0;
    }

    close $log_fd;

    $commits ||= 0, $insertions ||= 0, $deletions ||= 0;

    run [qw(git -c log.showSignature=false log --no-merges --name-only
            --pretty=), "--author=$user", @since], '>', \$log;

    $files = grep !/^$/, uniq split "\n", $log;

    $user = 'All Users' unless $user;
    print <<"EOT";
$user
    Files      : $files
    Commits    : $commits
    Insertions : $insertions
    Deletions  : $deletions

EOT
}
