#!/usr/bin/env perl

# Basic Search

use strict;
use warnings;

# User-Serviceable Parts
my @ignore_directories = qw(.git __pycache__ cover_db);
my @find_types = qw(f);
# End of User-Serviceable Parts

my $me = (split m{/}, $0)[-1];

@ARGV or die "Usage:\n\t$me <string pattern> [filename glob]...\n";

my $gitroot;
$gitroot = eval { `git rev-parse --show-toplevel` };
chomp $gitroot;
die "$me: Unsupported working directory or `git` not on \$PATH!\n"
    if $@ or not $gitroot;
chdir $gitroot;

my $search = shift;
my (@name, @path, @type);

push @name, (m{/} ? '-ipath' : '-iname', "*$_*", '-o') foreach @ARGV;
if (@name) { pop @name; unshift @name, '('; push @name, ')'; }

push @path, '-path', "*/$_", qw(-prune -o) foreach @ignore_directories;
if (@path) { pop @path; unshift @path, qw[! (]; push @path, ')'; }

push @type, '-type', $_, qw(-o) foreach @find_types;
if (@type) { pop @type; unshift @type, '('; push @type, ')'; }

exec qw(find .), @path, @type, @name, qw(-exec egrep -HnI --), $search, qw({} +);
